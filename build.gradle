plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'maven'
    id 'org.ajoberstar.grgit' version '1.7.1'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

version = "0.1"
group = "info.openmods.calc"
archivesBaseName = "calc"

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'com.google.guava:guava:17.0'
    compile 'org.apache.commons:commons-lang3:3.3.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.mockito:mockito-core:2.8.9"
}

def env = System.getenv()
def repo = org.ajoberstar.grgit.Grgit.open('.')

def timestamp(Date date) {
    TimeZone tz = TimeZone.getTimeZone("UTC");
    java.text.DateFormat df = new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'");
    df.setTimeZone(tz);
    return df.format(date);
}

def mf = manifest {
    if (env.BUILD_TAG != null) { // If this works, we'll assume we're in Jenkins atleast.
        attributes("Jenkins-Build": "true",
                   "Jenkins-Tag": env.BUILD_TAG,
                   "Jenkins-ID": env.BUILD_ID)
    } else {
        attributes("Jenkins-Build": "false")
    }

    attributes(
        "Built-Date": timestamp(new Date()),
        "Build-Java-Version": System.properties['java.version'],
        "Build-Java-Vendor": System.properties['java.vendor'],
        "Git-Branch": repo.branch.getCurrent().getName(),
        "Git-Hash": repo.head().getAbbreviatedId(8),
    )
}

jar {
    manifest {
        from mf
    }
}

task wrapper (type: Wrapper) {
    gradleVersion = "2.12"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.java

    manifest {
        from mf
    }
}

artifacts {
    archives sourcesJar
}

def ARTIFACT_NAME = 'calc'

uploadArchives {
    repositories.mavenDeployer {

        dependsOn 'build'
        repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())

        pom {
            groupId = "info.openmods"
            version = version
            artifactId = ARTIFACT_NAME

            project {
                name ARTIFACT_NAME
                packaging 'jar'
                description 'OpenModsLib calc functionality'
                url 'https://github.com/boq/OpenCalc'

                scm {
                    url 'https://github.com/boq/OpenCalc'
                    connection 'scm:git:git@github.com:boq/OpenCalc.git'
                    developerConnection 'scm:git:git@github.com:boq/OpenCalc.git'
                }

                issueManagement {
                    system 'github'
                    url 'https://github.com/boq/OpenCalc/issues'
                }

                licenses {
                    license {
                        name 'MIT'
                        url 'https://github.com/boq/OpenCalc/blob/master/LICENSE'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'boq'
                        name 'boq'
                        roles { role 'developer' }
                    }
                }
            }
        }
    }
}